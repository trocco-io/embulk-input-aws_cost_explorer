plugins {
    id "java"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "com.palantir.git-version" version "0.12.3"
    id "com.diffplug.spotless" version "6.13.0"
}

repositories {
    mavenCentral()
}

configurations {
    provided
}

group = "io.trocco"
description = "An embulk input plugin to load records from Aws Cost Explorer."
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]?/) {
        vd.lastTag
    }
    else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    def embulkVersion = "0.10.43"

    compileOnly "org.embulk:embulk-api:${embulkVersion}"
    compileOnly "org.embulk:embulk-spi:${embulkVersion}"

    compile "org.embulk:embulk-util-config:0.3.4"
    compile "org.embulk:embulk-util-timestamp:0.2.2"
    compile "com.amazonaws:aws-java-sdk-costexplorer:1.12.666"

    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-core:${embulkVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkVersion}"
    testImplementation "org.embulk:embulk-junit4:${embulkVersion}"
    testImplementation "org.mockito:mockito-core:3.12.4"
}

embulkPlugin {
    mainClass = "org.embulk.input.aws_cost_explorer.AwsCostExplorerInputPlugin"
    category = "input"
    type = "aws_cost_explorer"
}

clean { delete "classpath" }

gem {
  authors = ["kekekenta"]
  summary = "Aws Cost Explorer input plugin for Embulk"
  email = ["bell.kenta@gmail.com"]
  licenses = ["MIT"]
  homepage = "https://github.com/trocco-io/embulk-input-aws_cost_explorer"
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        // TODO: If we apply it, there will be many changes, so we will make it a separate Pull Request.
        // googleJavaFormat().aosp()
    }
}
