plugins {
    id "java"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "com.palantir.git-version" version "0.12.3"
    id "com.diffplug.spotless" version "6.13.0"
    id "checkstyle"
}

repositories {
    mavenCentral()
}

configurations {
    provided
}

group = "io.trocco"
description = "An embulk input plugin to load records from Aws Cost Explorer."
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]?/) {
        vd.lastTag
    }
    else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.embulk:embulk-core:0.9.23"
    provided "org.embulk:embulk-core:0.9.23"
    compile "com.amazonaws:aws-java-sdk-costexplorer:1.12.666"
    testCompile "junit:junit:4.+"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.input.aws_cost_explorer.AwsCostExplorerInputPlugin"
    category = "input"
    type = "aws_cost_explorer"
}

clean { delete "classpath" }

gem {
  authors = ["kekekenta"]
  summary = "Aws Cost Explorer input plugin for Embulk"
  email = ["bell.kenta@gmail.com"]
  licenses = ["MIT"]
  homepage = "https://github.com/trocco-io/embulk-input-aws_cost_explorer"
}
